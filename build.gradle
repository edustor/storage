group 'ru.edustor'
version '0.5'

buildscript {
    ext.kotlin_version = '1.0.5-2'
    ext.spring_boot_version = '1.4.1.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'spring-boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configurations {
    all*.exclude group: "log4j"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.1.0'
    }
}

compileKotlin {
    dependsOn('generateProto')
}

sourceSets {
    main {
        java {
            srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.protobuf:protobuf-java:3.1.0'
    compile 'com.google.protobuf:protobuf-java-util:3.1.0'
    compile 'io.minio:minio:2.0.4'

    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-amqp"
    compile 'com.googlecode.protobuf-java-format:protobuf-java-format:1.4'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0-M2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.0-M2"
    testCompile "org.mockito:mockito-core:2.+"
}